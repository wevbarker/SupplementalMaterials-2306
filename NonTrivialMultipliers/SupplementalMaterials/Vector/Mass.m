Section@{"Column 3 of",Cref@"VectorSectionTable",": Vector mass, but no axial multiplier"};
Subsection@"Setting up the Lagrangian";
Comment@{"We define the Lagrangian. It contains nonvanishing vector mass parameter",Inline@Mu2," but it does not contain a multiplier field",Inline@TLambda3[m],", which would ordinarily disable the axial vector torsion."};
Lagrangian=HoldForm[-(1/2)*MPl^2*RicciScalarGeoGaugeCovD[]+AlphaCoupling*(RicciGeoGaugeCovD[-a,-b]-RicciGeoGaugeCovD[-b,-a])*RicciGeoGaugeCovD[a,b]+Mu2*MPl^2*(T[i,-i,-m])*(T[j,-j,m])];

Lagrangian2=Lagrangian//=ReleaseHold;
Lagrangian2=Lagrangian2/.TSO13Activate;
Lagrangian2//=ToNewCanonical;
Lagrangian2//=CollectTensors;
DisplayExpression[Lagrangian2,EqnLabel->"VectorSectionyyInitialLagrangian"];

Lagrangian//=ReleaseHold;
Lagrangian//=Evaluate;
Lagrangian//=ToCanonical;

Comment@{"Now we would like to have the post-Riemannian decomposition of the Lagrangian",Cref@"VectorSectionyyInitialLagrangian","."};
Lagrangian=Lagrangian/.RicciGeoGaugeCovDToGeometric;
Lagrangian//=ToGeoCanonical;
Lagrangian=Lagrangian/.RicciScalarGeoGaugeCovDToGeometric;
Lagrangian//=ToGeoCanonical;
DisplayExpression[Lagrangian,EqnLabel->"VectorSectionyyPostRiemannianExpansion"];

Comment@{"We want to study the theory when it is linearised. As an intermediate step in order to do this, we just keep in",Cref@"VectorSectionyyPostRiemannianExpansion"," the second-order terms in torsion and no higher. Also from this point onwards we completely neglect factors of the curvature which may arise in the field equations by commuting covariant derivatives."};
PertT=MakeRule[{T[a,-m,-n],PertParam*T[a,-m,-n]},MetricOn->All,ContractMetrics->True];
Lagrangian=Lagrangian/.PertT;
Lagrangian=Series[Lagrangian,{PertParam,0,2}];
Lagrangian//=Normal;
Lagrangian=Lagrangian/.{PertParam->1};
Lagrangian//=ToGeoCanonical;
DisplayExpression[Lagrangian,EqnLabel->"VectorSectionyyLinearPostRiemannianExpansion"];

Comment@{"Now we decompose the torsion in",Cref@"VectorSectionyyLinearPostRiemannianExpansion"," into the Lorentz irreps."};
WholeLagrangian=Lagrangian;
Lagrangian=Lagrangian/.TSO13Activate;
Lagrangian//=ToGeoCanonical;
DisplayExpression[Lagrangian,EqnLabel->"VectorSectionyyPostRiemannianExpansion"];

Subsection@"Manipulating the field equations";
Comment@"Here is the tensor field equation.";
Expr=VarD[T[a,-b,-c],GeoCovD][WholeLagrangian];
Expr=Expr/.TSO13Activate;
Expr//=ToGeoCanonical;
Expr=PT1[i,-j,-k,a,-b,-c]*Expr;
Expr=Expr/.PActivate;
Expr//=ToGeoCanonical;
Expr=Expr/.GradDivT2ToDivGradT2;
Expr=Expr/.GradDivT3ToDivGradT3;
Expr=Expr/.ToMaxwell;
Expr//=ToGeoCanonical;
Expr//=ToGeoCanonical;
Expr=Expr/.{RicciScalarGeoCovD->Zero,RicciGeoCovD->Zero,RiemannGeoCovD->Zero};
DisplayEquation[Expr,EqnLabel->"VectorSectionyyEqn1"];
Eqn1s=Expr;
Comment@{"There is some nuance to how we obtain",Cref@"VectorSectionyyEqn1",", in that we can't just vary with respect to the tensor irrep. If we do that, then the resulting equation can have traces which are not true on-shell. It is safest to actually vary with respect to the whole torsion tensor and then project out the (traceless by construction) tensor irrep from that equation."};

Comment@"Now for the vector equation.";
Expr=VarD[T2[-m],GeoCovD][Lagrangian];
Expr//=ToGeoCanonical;
Expr=Expr/.GradDivT2ToDivGradT2;
Expr=Expr/.GradDivT3ToDivGradT3;
Expr=Expr/.ToMaxwell;
Expr//=ToGeoCanonical;
Expr//=ToGeoCanonical;
Expr=Expr/.{RicciScalarGeoCovD->Zero,RicciGeoCovD->Zero,RiemannGeoCovD->Zero};
DisplayEquation[Expr,EqnLabel->"VectorSectionyyEqn2"];
Eqn2=Expr;

Comment@"And for the axial vector equation.";
Expr=VarD[T3[-m],GeoCovD][Lagrangian];
Expr//=ToGeoCanonical;
Expr=Expr/.GradDivT2ToDivGradT2;
Expr=Expr/.GradDivT3ToDivGradT3;
Expr=Expr/.ToMaxwell;
Expr//=ToGeoCanonical;
Expr//=ToGeoCanonical;
Expr=Expr/.{RicciScalarGeoCovD->Zero,RicciGeoCovD->Zero,RiemannGeoCovD->Zero};
DisplayEquation[Expr,EqnLabel->"VectorSectionyyEqn3"];
Eqn3=Expr;

Comment@{"Now we can also take the dual of",Cref@"VectorSectionyyEqn3",", and so we write this out for completeness."};
Eqn3b=ReplaceDummies[Expr]*epsilonGeoG[-m,a,b,c];
Eqn3b//=ToGeoCanonical;
Eqn3b=Eqn3b/.ToMaxwell;
Eqn3b//=ToGeoCanonical;
Eqn3b=Eqn3b/.{RicciScalarGeoCovD->Zero,RicciGeoCovD->Zero,RiemannGeoCovD->Zero};
DisplayEquation[Eqn3b,EqnLabel->"VectorSectionyyEqn3s"];

Comment@{"With the effective 2-form field in",Cref@"KalbRamondDef",", we are ready to simplify the equations of motion."};
Comment@{"Here is the vector equation",Cref@"VectorSectionyyEqn2","."};
Expr=Eqn2;
Expr=Expr/.ToNonlinearMaxwell;
Expr//=ToNewCanonical;
Expr=Expr/.Bianchi;
Expr//=CollectTensors;
DisplayEquation[Expr,EqnLabel->"VectorSectionyyEqn2j"];
T2ToB=MakeQuotientRule[{T2[m],Expr},Method->"Coefficient"];
(*T2ToB=MakeQuotientRule[{TLambda2[m],Expr},Method->"Coefficient"];*)

Comment@{"Here is the axial vector equation",Cref@"VectorSectionyyEqn3","."};
Expr=Eqn3;
Expr=Expr/.ToNonlinearMaxwell;
Expr//=ToNewCanonical;
Expr=Expr/.Bianchi;
Expr//=CollectTensors;
DisplayEquation[Expr,EqnLabel->"VectorSectionyyEqn3j"];
T3ToB=MakeQuotientRule[{T3[m],Expr},Method->"Coefficient"];
Eqn3=Expr;

Comment@{"Again here is the dual part of",Cref@"VectorSectionyyEqn3j"};
Expr=epsilonGeoG[-m,-i,-j,-k]*Eqn3;
Expr=Expr/.ToNonlinearMaxwell;
Expr//=ToNewCanonical;
Expr=Expr/.Bianchi;
Expr//=CollectTensors;
DisplayEquation[Expr,EqnLabel->"VectorSectionyyEqn3sj"];
Comment@{"Note that",Cref@{"VectorSectionyyEqn2j","VectorSectionyyEqn3j"}," allow us to solve for",Inline@T3[-m]," and",Inline@T2[m]," purely in terms of the 2-form."};

Comment@{"And here is the tensor equation",Cref@"VectorSectionyyEqn1","."};
Expr=Eqn1s;
Expr=Expr/.ToNonlinearMaxwell;
Expr//=ToNewCanonical;
Expr=Expr/.Bianchi;
Expr//=CollectTensors;
Expr=Expr/.ToNonlinearMaxwellb;
Expr//=ToNewCanonical;
Expr=Expr/.Bianchi;
Expr//=CollectTensors;
Expr=Expr/.MaxwellToDual3;
Expr//=ToNewCanonical;
Expr//=CollectTensors;
Expr=Expr/.DualToMaxwell3;
Expr//=ToNewCanonical;
Expr//=CollectTensors;
Expr=Expr/.Bianchi;
Expr//=ToNewCanonical;
Expr=Expr/.{RicciScalarGeoCovD->Zero,RicciGeoCovD->Zero,RiemannGeoCovD->Zero};
Expr//=CollectTensors;
DisplayEquation[Expr,EqnLabel->"VectorSectionyyEqn1j"];

Comment@{"Now the next thing we do is to take the divergence of",Cref@"VectorSectionyyEqn1j","."};
Expr//=GeoCovD[-i];
Expr//=ToNewCanonical;
Expr//=CollectTensors;
DisplayEquation[Expr,EqnLabel->"VectorSectionyyEqn1k"];

Comment@{"Next we substitute into",Cref@"VectorSectionyyEqn1k"," for the 2-form field in",Cref@"KalbRamondDef"," again."};
Expr=Expr/.ToNonlinearMaxwell;
Expr//=ToNewCanonical;
Expr=Expr/.Bianchi;
Expr//=CollectTensors;
Expr=Expr/.ToNonlinearMaxwellb;
Expr//=ToNewCanonical;
Expr=Expr/.Bianchi;
Expr//=CollectTensors;
Expr=Expr/.MaxwellToDual3;
Expr//=ToNewCanonical;
Expr//=CollectTensors;
Expr=Expr/.DualToMaxwell3;
Expr//=ToNewCanonical;
Expr//=CollectTensors;
Expr=Expr/.Bianchi;
Expr//=ToNewCanonical;
Expr//=CollectTensors;
DisplayEquation[Expr,EqnLabel->"VectorSectionyyEqn1l"];

Comment@{"Next we will take",Cref@"VectorSectionyyEqn1l"," and expand the vector and axial Maxwell tensors back into derivatives, and substitute for the solutions in terms of the 2-form field that we obtained from",Cref@{"VectorSectionyyEqn2j","VectorSectionyyEqn3j"},"."};
Expr=Expr/.FromMaxwell;
Expr=Expr/.T2ToB;
Expr=Expr/.T3ToB;
Expr//=ToNewCanonical;
Expr//=CollectTensors;
Expr//=SortCovDs;
(*Expr=Expr/.{RicciScalarGeoCovD->Zero,RicciGeoCovD->Zero,RiemannGeoCovD->Zero};*)
Expr=Expr/.{RicciScalarGeoCovD->Zero,RicciGeoCovD->Zero,RiemannGeoCovD->Zero};
Expr//=ToNewCanonical;
Expr//=CollectTensors;
DisplayEquation[Expr,EqnLabel->"VectorSectionyyEqn1p"];
Expr2=Expr;

Comment@{"So now the equation in",Cref@"VectorSectionyyEqn1p"," contains all the dynamical information about the linear spectrum of the theory",Cref@"VectorSectionyyInitialLagrangian",", since",Cref@{"VectorSectionyyEqn2j","VectorSectionyyEqn3j","VectorSectionyyEqn1j"}," serve only to determine the torsion in terms of the 2-form. So the key question is how much of the 2-form does",Cref@"VectorSectionyyEqn1p"," propagate?"};
Comment@{"Let's first take the divergence of",Cref@"VectorSectionyyEqn1p","."};
Expr//=GeoCovD[j];
Expr//=SortCovDs;
Expr=Expr/.{RicciScalarGeoCovD->Zero,RicciGeoCovD->Zero,RiemannGeoCovD->Zero};
Expr//=ToNewCanonical;
Expr//=CollectTensors;
Expr//=SortCovDs;
Expr=Expr/.{RicciScalarGeoCovD->Zero,RicciGeoCovD->Zero,RiemannGeoCovD->Zero};
Expr//=ToNewCanonical;
Expr//=CollectTensors;
Expr=Expr/.Constraints;
Expr//=ToNewCanonical;
Expr//=CollectTensors;
DisplayEquation[Expr,EqnLabel->"VectorSectionyyEqn1x"];
Comment@{"So",Cref@"VectorSectionyyEqn1x"," still looks like a propagating equation."};
Comment@{"Let's now take the dual of the gradient of",Cref@"VectorSectionyyEqn1p","."};
Expr=Expr2;
Expr=epsilonGeoG[v,j,k,q]*GeoCovD[-q]@Expr;
Expr//=SortCovDs;
Expr=Expr/.{RicciScalarGeoCovD->Zero,RicciGeoCovD->Zero,RiemannGeoCovD->Zero};
Expr//=ToNewCanonical;
Expr//=CollectTensors;
Expr//=SortCovDs;
Expr=Expr/.{RicciScalarGeoCovD->Zero,RicciGeoCovD->Zero,RiemannGeoCovD->Zero};
Expr//=ToNewCanonical;
Expr//=CollectTensors;
Expr=Expr/.Constraints;
Expr//=ToNewCanonical;
Expr//=CollectTensors;
DisplayEquation[Expr,EqnLabel->"VectorSectionyyEqn1y"];
Comment@{"So",Cref@"VectorSectionyyEqn1y"," is just a constraint, which knocks out 3 d.o.f from the 2-form."};
Manuscript@{"Since we lose 3 d.o.f to a constraint, we conclude that half the 2-form propagates, i.e. 3 propagating d.o.f in the linear spectrum."};
Manuscript@{"We won't compute the last column in",Cref@"VectorSectionTable"," yet: instead we'll transition to the equivalent table for which the axial and vector torsion are switched, and for which we are validating all our nonlinear spectral claims with full Hamiltonian analyses, and study it in that more thorough context."};
